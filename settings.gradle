pluginManagement {
    gradle.ext.kotlinVersion = '1.8.22'
    gradle.ext.daggerVersion = '2.44'
    gradle.ext.agpVersion = '8.1.0'
    gradle.ext.detektVersion = '1.19.0'
    gradle.ext.automatticPublishToS3Version = '0.8.0'

    plugins {
        id 'io.gitlab.arturbosch.detekt' version gradle.ext.detektVersion
        id 'org.jetbrains.kotlin.android' version gradle.ext.kotlinVersion
        id 'org.jetbrains.kotlin.jvm' version gradle.ext.kotlinVersion
        id 'org.jetbrains.kotlin.plugin.parcelize' version gradle.ext.kotlinVersion
        id 'org.jetbrains.kotlin.kapt' version gradle.ext.kotlinVersion
        id "com.android.application" version gradle.ext.agpVersion
        id "com.android.library" version gradle.ext.agpVersion
        id 'dagger.hilt.android.plugin'
        id "com.automattic.android.publish-to-s3" version gradle.ext.automatticPublishToS3Version
    }
    repositories {
        maven {
            url 'https://a8c-libs.s3.amazonaws.com/android' 
            content {
                includeGroup "com.automattic.android"
                includeGroup "com.automattic.android.publish-to-s3"
            }
        }
        google()
        gradlePluginPortal()
    }
    resolutionStrategy {
        eachPlugin {
            if (requested.id.id.contains("hilt")) {
                useModule("com.google.dagger:hilt-android-gradle-plugin:$gradle.ext.daggerVersion")
            }
        }
    }
}

rootProject.name = "Media Picker"
include ':mediapicker',
        ':mediapicker:domain',
        ':mediapicker:source-device',
        ':mediapicker:source-camera',
        ':mediapicker:source-gif',
        ':mediapicker:source-wordpress',
        ':sampleapp'

// Build cache is only enabled for CI, at least for now
if (System.getenv().containsKey("CI")) {
    buildCache {
        remote(HttpBuildCache) {
            url = "http://10.0.2.215:5071/cache/"
            allowUntrustedServer = true
            allowInsecureProtocol = true
            push = true
        }
    }
}
