pluginManagement {
    gradle.ext.kotlinVersion = '1.5.31'
    gradle.ext.daggerVersion = '2.40'

    plugins {
        id 'org.jetbrains.kotlin.android' version gradle.ext.kotlinVersion
        id 'org.jetbrains.kotlin.jvm' version gradle.ext.kotlinVersion
        id 'org.jetbrains.kotlin.plugin.parcelize' version gradle.ext.kotlinVersion
        id 'org.jetbrains.kotlin.kapt' version gradle.ext.kotlinVersion
        id 'dagger.hilt.android.plugin'
    }
    repositories {
        google()
        gradlePluginPortal()
        jcenter()
    }
    resolutionStrategy {
        eachPlugin {
            if (requested.id.id.contains("com.android")) {
                useModule("com.android.tools.build:gradle:4.2.2")
            }
            else if (requested.id.id.contains("hilt")) {
                useModule("com.google.dagger:hilt-android-gradle-plugin:$gradle.ext.daggerVersion")
            }
        }
    }
}

rootProject.name = "Media Picker"
include ':mediapicker',
        ':mediapicker:domain',
        ':mediapicker:source-device',
        ':mediapicker:source-gif',
        ':mediapicker:source-wordpress',
        ':sampleapp'

// Build cache is only enabled for CI, at least for now
if (System.getenv().containsKey("CI")) {
    buildCache {
        remote(HttpBuildCache) {
            url = "http://10.0.2.215:5071/cache/"
            allowUntrustedServer = true
            allowInsecureProtocol = true
            push = true
        }
    }
}
