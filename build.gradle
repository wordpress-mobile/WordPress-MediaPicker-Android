import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'io.gitlab.arturbosch.detekt' apply false
    id 'com.android.application' apply false
    id 'com.android.library' apply false
    id 'org.jetbrains.kotlin.android' apply false
    id 'dagger.hilt.android.plugin' apply false
    id "com.automattic.android.publish-to-s3" apply false
}

allprojects {
    apply plugin: 'io.gitlab.arturbosch.detekt'

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://www.jitpack.io" }
        maven { url "https://a8c-libs.s3.amazonaws.com/android" }
    }

    detekt {
        config = files("${project.rootDir}/config/detekt/detekt.yml")
        autoCorrect = false
        buildUponDefaultConfig = true
        disableDefaultRuleSets = false
        ignoreFailures = false
        parallel = true
        debug = false
    }

    tasks.withType(KotlinCompile).all {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
            allWarningsAsErrors = true
            freeCompilerArgs += [
                    "-opt-in=kotlin.RequiresOptIn",
                    "-Xjvm-default=all-compatibility",
            ]
        }
    }
}

subprojects {
    repositories {
        flatDir {
            dirs '../../aars'
        }
    }
}

ext {
    minSdkVersion = 24
    compileSdkVersion = 34
    targetSdkVersion = 34
}

ext {
    // libs
    wordPressFluxCVersion = 'trunk-7ea7465680431fa4df5f0433d4c8b8395055b997'
    wordpressUtilsVersion = "2.6.0"

    // main
    androidxAppcompatVersion = '1.4.2'
    androidxCoreVersion = '1.12.0'
    androidxDatastoreVersion = '1.0.0'
    androidxConstraintlayoutVersion = '2.1.4'
    androidxLifecycleVersion = '2.4.1'
    androidxNavigationVersion = '2.5.3'
    androidxSwipeToRefreshVersion = '1.1.0'
    chrisbanesPhotoviewVersion = '2.3.0'
    glideVersion = '4.13.2'
    googleMaterialVersion = '1.6.1'
    kotlinxCoroutinesVersion = '1.6.4'
    squareupRetrofitVersion = "2.9.0"

    // other
    wordpressLintVersion = "2.0.0"

    // project
    mediaPickerDomainDependency = project.hasProperty("mediaPickerDomainVersion") ? "org.wordpress.mediapicker:domain:${project.getProperty("mediaPickerDomainVersion")}" : project(":mediapicker:domain")
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$gradle.ext.detektVersion"
}
