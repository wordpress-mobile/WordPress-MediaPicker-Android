plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.parcelize'
    id 'org.jetbrains.kotlin.kapt'
    id 'dagger.hilt.android.plugin'
    id 'maven-publish'
    id "com.automattic.android.publish-to-s3"
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        versionCode 1
        versionName "0.1"
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    lintOptions {
        warningsAsErrors true
    }

    dependencies {
        api mediaPickerDomainDependency

        implementation "com.google.android.material:material:$materialVersion"

        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

        implementation "androidx.navigation:navigation-fragment-ktx:$navigationFragmentVersion"
        implementation "androidx.core:core-ktx:$coreVersion"
        implementation "androidx.appcompat:appcompat:$appcompatVersion"
        implementation "androidx.constraintlayout:constraintlayout:$constraintlayoutVersion"
        implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swiperefreshlayoutVersion"
        implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
        implementation "androidx.datastore:datastore-preferences:$datastoreVersion"

        implementation "com.github.bumptech.glide:glide:$glideVersion"

        implementation "com.google.dagger:hilt-android:$daggerVersion"
        implementation "com.google.dagger:hilt-android-compiler:$daggerVersion"
        kapt "com.google.dagger:hilt-compiler:$daggerVersion"

        implementation "com.github.chrisbanes:PhotoView:$photoViewVersion"

        implementation "org.wordpress:utils:$wordpressUtilsVersion"
    }
}

project.afterEvaluate {
    publishing {
        publications {
            MediaPickerPublication(MavenPublication) {
                from components.release

                groupId "org.wordpress"
                artifactId "mediapicker"
                artifact tasks.named("androidSourcesJar") // This task is added by 'publish-to-s3' plugin
                // version is set by 'publish-to-s3' plugin
            }
        }
   }
}
